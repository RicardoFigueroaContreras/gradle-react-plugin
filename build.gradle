buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java-gradle-plugin'

version = '0.1'
group = 'net.eikehirsch.react'

def compatibilityVersion = 1.7
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'com.moowork.gradle:gradle-node-plugin:0.7'
    testCompile 'junit:junit:4.11'
}

task sourcesJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task groovydocJar( type: Jar ) {
    classifier 'javadoc'
    from groovydoc
}

artifacts {
    archives sourcesJar
    archives groovydocJar
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourcesJar
            artifact groovydocJar
        }
    }
}

bintray {
    user = project.hasProperty( 'bintrayUser' ) ? project.getProperty( 'bintrayUser' ) : null
    key = project.hasProperty( 'bintrayAPIKey' ) ? project.getProperty( 'bintrayAPIKey' ) : null

    publications = ['mavenJava']
    publish = true

    dryRun = true

    pkg {
        repo = 'maven'
        name = 'gradle-react-plugin'
        licenses = ['Apache-2.0']
        desc = 'Gradle plugin for compiling jsx sources to javascript. '
        websiteUrl = 'https://github.com/ehirsch/gradle-react-plugin'
        issueTrackerUrl = 'https://github.com/ehirsch/gradle-react-plugin/issues'
        vcsUrl = 'https://github.com/ehirsch/gradle-react-plugin.git'
        labels = ['java', 'gradle', 'groovy', 'node', 'react', 'jsx']
        publicDownloadNumbers = true
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}